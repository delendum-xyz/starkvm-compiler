// WASM opcodes to AST
class Wasm {

typedef bindata_t = varray[uint8];
fun get (x:bindata_t) (i:int) :uint8 => x.(i.size);

fun uleb128(x:bindata_t) (var i:int) : int * uint64 {
  var y = 0.uint64;
  var shift = 0.uint64;
  while x.i \& 0x80.uint8 > 0.uint8 do
   y = y \| ((x.i.uint64 \& 0x7F.uint64) << shift);
    shift = shift + 7.uint64;
    ++i;
  done
  y = y \| x.i.uint64 << shift; 
  return i+1,y;
}

fun ileb128(x:bindata_t) (i:int) : int * int64 {
  var j,y = uleb128 x i;
  if x.(j - 1) \& 0x40.uint8 > 0.uint8 perform
    y = y \| (~0.uint64 << ((j - i).uint64 * 7.uint64));
  return j,C_hack::cast[int64] y; 
}

fun hexb (x:uint8) : string {
  fun nib (x:uint8) => if x < 10.uint8 then char (x + "0".char.ord.uint8) else char (x - 10.uint8 + "A".char.ord.uint8);
  var upper =  (x \& 0xF0.uint8) >> 4u8;   
  var lower =  x \& 0x0F.uint8;
  return "0x" + upper.nib + lower.nib; 
}

// Note: section 0 is the header NOT a custom section.
// We skip custom sections
typedef section_index_t = (int * int) ^ 12;
  
fun parse_module_structure (inp: bindata_t) (idx: int) : section_index_t {
  var tot = inp.len.int - 1;
  var x : section_index_t;
  for j in 0..11 perform x&.j <- (0,0); 
  var start = 8;
  while start < tot do
    var i = start;
    var secbyte = inp.i.uint8;
    ++i;
    def i, var n = uleb128 inp i;
    if secbyte > 0u8 perform
      x&.secbyte <- (i, n.int);
    start = i + n.int;
    if start > tot do
      println$ "Malformed module, section " + secbyte.str + " overflows data size " + tot.str;
      System::exit 1; 
    done 
  done
  if start != tot do
    println$ "Malformed module, final index " + start.str + " should equal file size " + tot.str;
    System::exit 1;
  done
  return x;
}

fun section_name (i:int) : string => 
  match i with
  | 0 => "custom"
  | 1 => "type"
  | 2 => "import"
  | 3 => "function"
  | 4 => "table"
  | 5 => "memory"
  | 6 => "global"
  | 7 => "export"
  | 8 => "start"
  | 9 => "element"
  | 10 => "code"
  | 11 => "data"
  | 12 => "data count"
;

typedef limits = (
  | `min_limit of  uint32 // 0x00
  | `range_limit of uint32 * uint32 // 0x01
);

fun parse_limits (inp: bindata_t) (start:int) : int * limits =>
  match inp.get start with 
  | 0x00u8 => 
    let k,n = uleb128 inp (start + 1) in
    k,(`min_limit n.uint32 :>> limits)
  | 0x01u8 => 
    let k1,n1 = uleb128 inp (start + 1) in
    let k2,n2 = uleb128 inp k1.int in
    k2,(`range_limit (n1.uint32, n2.uint32) :>> limits)
; 
  
typedef vec[T] = varray[T];

fun parse_vec[T] (parseT: bindata_t -> int -> int * T) 
  (inp:bindata_t) (start:int) : int * vec[T] 
{
   var j, n = uleb128 inp start;
   var out = varray[T] (n.size);
   for i in 0..n.int - 1 do
    def j, var v = parseT inp j;
    push_back (out,v);
  done
  return j,out;
}


typedef vectype = (
  | `vectype
);
typedef numtype = (
  | `i32
  | `i64
  | `f32
  | `f64
);
 
typedef reftype = (
  | `funcref
  | `externref
);


fun parse_reftype (inp: bindata_t) (start: int) : int * reftype {
  var c = inp.get start;
  return 
    start + 1,
    match c with
    | 0x70u8 => (#`funcref :>> reftype)
    | 0x6Fu8 => (#`externref :>> reftype)
    endmatch
  ;
}

typedef valuetype = (
  | numtype
  | vectype
  | reftype
);
instance Str[valuetype] { fun str (x:valuetype) => x._strr; }

fun parse_valuetype (inp:bindata_t) (start:int) : int * valuetype {
  var c = inp.get start;
  //println$ "Input code " + c.hexb; 
  var vt = match c with
  | 0x7Fu8 => #`i32 :>> valuetype
  | 0x7Eu8 => #`i64 :>> valuetype
  | 0x7Du8 => #`f32 :>> valuetype
  | 0x7Cu8 => #`f64 :>> valuetype
  | 0x7Bu8 => #`vectype :>> valuetype
  | 0x70u8 => #`funcref :>> valuetype
  | 0x6Fu8 => #`externref :>> valuetype
  endmatch;
  return start + 1, vt;
}

typedef functype = ( 
 | `functype of vec[valuetype] * vec[valuetype] 
);

typedef memtype = (
 | `memtype of limits
);

fun parse_memtype (inp: bindata_t) (start: int) : int * memtype {
  var nxt, lim = parse_limits inp start;
  return nxt, `memtype lim;
}

typedef mut = (
  | `const             // 0x00
  | `var               // 0x01
);

fun parse_mut (inp: bindata_t) (start: int) : int * mut {
  var c = inp.get start;
  return 
    start + 1,
    match c with
    | 0x00u8 => #`const :>> mut
    | 0x01u8 => #`var :>> mut
    endmatch
  ;
}

typedef globaltype = (
  | `globaltype of mut * valuetype
);

fun parse_globaltype (inp: bindata_t) (start: int) : int * globaltype {
  var i, vt = parse_valuetype inp start;
  var j, mt = parse_mut inp i;
  return j,`globaltype (mt, vt);
}

typedef tabletype = (
  `tabletype of reftype * limits
);

fun parse_tabletype (inp: bindata_t) (start: int) : int * tabletype {
  var i,rt = parse_reftype inp start;
  var j,lim = parse_limits inp i;
  return j,`tabletype (rt,lim);
}

fun parse_functype (inp: bindata_t) (start: int) : int * functype { 
  var ftcode = inp.get start; 
  assert ftcode == 0x60.uint8;
  var cstart, domain = parse_vec[valuetype] parse_valuetype inp (start+1);
  var cend, codomain = parse_vec[valuetype] parse_valuetype inp cstart;
  return cend,`functype (domain,codomain);
}

fun parse_type_section (inp: bindata_t) (var start:int, span:int) : varray[functype] {
  var endidx = start + span;
  var i, n = uleb128 inp start;
  println$ "N fun types = " + n.str;
  var fts = varray[functype] (n.size);
  for j in 0 ..  n.int - 1 do
    def i, var ft = parse_functype inp i;
    push_back (fts, ft);
  done
  if i != endidx do
    println$ "Malformed function type section, final index " + i.str + " should equal " + endidx.str;
    System::exit 1;
  done
  return fts;
}

fun parse_byte (inp: bindata_t) (start: int) : int * uint8 =>
  let v = inp.get start in
  start + 1, v
;

typedef name = (
| `name of varray[uint8]
);

instance Str[name] {
  fun str (x:name) : string {
    var s = "";
    match x with
    | `name v => 
      for u in v do
         s += u.int.char;
      done
    endmatch;
    return s;
  }
}
 
fun parse_name (inp: bindata_t) (start: int) : int * name {
  var i, nam = parse_vec[uint8] parse_byte inp start;
  return i, `name nam;
}

typedef importdesc = (
  | `imp_func of int // type index
  | `imp_table of tabletype 
  | `imp_mem of memtype 
  | `imp_global of globaltype
);

fun parse_importdesc (inp: bindata_t) (start: int) : int * importdesc {
  var impcode = inp.get start;
  match impcode with
  | 0x00u8 => 
    var nxt0, idx = uleb128 inp (start+1); 
    return nxt0, (`imp_func idx.int :>> importdesc);

  | 0x01u8 => 
    var nxt1, ttype = parse_tabletype inp (start + 1);
    return nxt1, (`imp_table ttype :>> importdesc);

  | 0x02u8 => 
    var nxt2, mtype = parse_memtype inp (start + 1);
    return nxt2, (`imp_mem mtype :>> importdesc);

  | 0x03u8 => 
    var nxt3, gtype = parse_globaltype inp (start + 1);
    return nxt3, (`imp_global gtype :>> importdesc);
  endmatch;
}

typedef import = (
  | `import of name * name * importdesc
);

instance Str[import] {
  fun str: import -> string = 
  | `import (m,s,d) =>
     m.str + "::" + s.str + " : " +d._strr
  ;
}

fun parse_import (inp: bindata_t) (start: int) : int * import {
  var i, modname = parse_name inp start;
  var j, importname = parse_name inp i;
  var k, desc = parse_importdesc inp j;
  return k,`import (modname, importname, desc);
}
 
fun parse_import_section (inp: bindata_t) (var start:int, span:int) : varray[import] {
  var endidx = start + span;
  var i, n = uleb128 inp start;
  println$ "N imports = " + n.str;
  var fts = varray[import] (n.size);
  for j in 0 ..  n.int - 1 do
    def i, var ft = parse_import inp i;
    push_back (fts, ft);
  done
  if i != endidx do
    println$ "Malformed function import section, final index " + i.str + " should equal " + endidx.str;
    System::exit 1;
  done
  return fts;
}



} // class

/*  test cases from Wikipedia:

begin 
  var x = varray (0xE5.uint8,0x8E.uint8,0x26.uint8);
  println$ x;
  var i = 0;
  var j, y = Wasm::uleb128 x i;
  println$ j,y; // 624485
end
begin 
  var x = varray (0xC0.uint8,0xBB.uint8,0x78.uint8);
  println$ x;
  var i = 0;
  var j, y = Wasm::ileb128 x i;
  println$ j,y; // -12345
end

*/
